#
# A theme based steeef.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Erik J. Sturcke
#

function prompt_simpleline_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="⟨${_prompt_simpleline_colors[5]}%b%f%u%c${_prompt_simpleline_colors[4]}•%f⟩"
  else
    branch_format="⟨${_prompt_simpleline_colors[5]}%b%f%u%c⟩"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'
}

function prompt_simpleline_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_simpleline_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_simpleline_colors=(
      "%F{4}"   # Faded blue (line)
      "%F{2}"   # Green (user name/host)
      "%F{12}"  # Stronger blue (path) 
      "%F{1}"   # Orange (git status/prompt)
      "%F{3}"   # Yellow (git branch)
      "%F{3}"   # Yellow (time)
    )
  else
    _prompt_simpleline_colors=(
      "%F{blue}"
      "%F{green}"
      "%F{blue}"
      "%F{red}"
      "%F{yellow}"
      "%F{gray}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="${_prompt_simpleline_colors[5]}⟨%b%u%c)%f"
  local action_format="${_prompt_simpleline_colors[1]}|%a|${_prompt_simpleline_colors[5]} "
  local unstaged_format="${_prompt_simpleline_colors[4]}•${_prompt_simpleline_colors[5]}"
  local staged_format="${_prompt_simpleline_colors[2]}•${_prompt_simpleline_colors[5]}"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format} "
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Define prompts.
  RPROMPT='${_prompt_simpleline_colors[6]}%T'
  PROMPT="${_prompt_simpleline_colors[1]}╭─ ${_prompt_simpleline_colors[3]}%n${_prompt_simpleline_colors[1]}·${_prompt_simpleline_colors[3]}%m ${_prompt_simpleline_colors[1]}── ${_prompt_simpleline_colors[2]}%~%f "'${vcs_info_msg_0_}'"${_prompt_simpleline_colors[1]}───────────────
${_prompt_simpleline_colors[1]}╰─ ${_prompt_simpleline_colors[4]}$%f "
}

prompt_simpleline_setup "$@"

